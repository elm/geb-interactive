apply plugin: 'groovy'
apply plugin: 'idea'

repositories {
    mavenCentral()
}

configurations {
    groovyShell
}

def gebVersion = '0.9.0'
def webdriverVersion = '2.46.0'

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.6'

    compile "org.gebish:geb-core:$gebVersion"
    compile "org.seleniumhq.selenium:selenium-support:$webdriverVersion"
    compile "org.seleniumhq.selenium:selenium-firefox-driver:$webdriverVersion"

    groovyShell 'commons-cli:commons-cli:1.2'
    groovyShell('jline:jline:2.11') {
        exclude(group: 'junit', module: 'junit')
    }
    groovyShell 'org.codehaus.groovy:groovy-groovysh:2.4.6'
}

// should be run with -q
task('geb-interactive', dependsOn: 'classes', type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath + configurations.groovyShell
    main = 'GroovyGebShellStarter'
    standardInput = System.in
    workingDir = 'src/main/groovy'
}